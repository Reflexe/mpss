// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT license.

#import <XCTest/XCTest.h>

#include "tests/test_entry.h"

#include <format>
#include <string>

#include <gtest/gtest.h>

@interface MPSSNativeTests : XCTestCase
@end

@implementation MPSSNativeTests

- (void)testNativeSuite
{
    char executable_name[] = "mpss_ios_xctest";
    char *argv[] = {executable_name, nullptr};
    const int result = mpss::tests::run_all_tests(1, argv);

    const auto *unit_test = ::testing::UnitTest::GetInstance();
    const int total = unit_test->test_to_run_count();
    const int passed = unit_test->successful_test_count();
    const int skipped = unit_test->skipped_test_count();
    const int failed = unit_test->failed_test_count();

    bool contract_failed = 0 != result || 0 != failed;
#ifdef MPSS_IOS_EXPECTED_TOTAL
    contract_failed = contract_failed || MPSS_IOS_EXPECTED_TOTAL != total || MPSS_IOS_EXPECTED_PASSED != passed ||
                      MPSS_IOS_EXPECTED_SKIPPED != skipped || MPSS_IOS_EXPECTED_FAILED != failed;
#endif
    if (contract_failed)
    {
#ifdef MPSS_IOS_EXPECTED_TOTAL
        const std::string message =
            std::format("Native GoogleTest suite contract failed: exit={}, total={}, passed={}, skipped={}, failed={}; "
                        "expected total={}, passed={}, skipped={}, failed={}",
                        result, total, passed, skipped, failed, MPSS_IOS_EXPECTED_TOTAL, MPSS_IOS_EXPECTED_PASSED,
                        MPSS_IOS_EXPECTED_SKIPPED, MPSS_IOS_EXPECTED_FAILED);
#else
        const std::string message =
            std::format("Native GoogleTest suite failed: exit={}, total={}, passed={}, skipped={}, failed={}", result,
                        total, passed, skipped, failed);
#endif
        NSString *description = [NSString stringWithUTF8String:message.c_str()];
        XCTIssue *issue = [[XCTIssue alloc] initWithType:XCTIssueTypeAssertionFailure
                                     compactDescription:description
                                    detailedDescription:nil
                                     sourceCodeContext:[[XCTSourceCodeContext alloc] init]
                                       associatedError:nil
                                           attachments:@[]];
        [self recordIssue:issue];
    }
}

@end
